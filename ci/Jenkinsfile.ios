pipeline {
  agent { label 'macos' }

  parameters {
    string(
      name: 'BUILD_TYPE',
      description: 'Specify build type. Values: pr / e2e / nightly / release',
      defaultValue: 'pr',
    )
  }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 45, unit: 'MINUTES')
    /* Limit builds retained */
    buildDiscarder(logRotator(
      numToKeepStr: '10',
      daysToKeepStr: '20',
      artifactNumToKeepStr: '10',
    ))
  }

  environment {
    LANG     = "en_US.UTF-8"
    LC_ALL   = "en_US.UTF-8"
    LANGUAGE = "en_US.UTF-8"
    TARGET_OS = 'ios'
    NIX_CONF_DIR = "${env.WORKSPACE}/nix"
    FASTLANE_DISABLE_COLORS = 1
    BUNDLE_PATH = "${HOME}/.bundle"
    /* We use EXECUTOR_NUMBER to avoid multiple instances clashing */
    LEIN_HOME   = "/var/tmp/lein-${EXECUTOR_NUMBER}"
  }

  stages {
    stage('Prep') {
      steps {
        script {
          /* Necessary to load methods */
          ios   = load 'ci/ios.groovy'
          cmn   = load 'ci/common.groovy'
          btype = cmn.utils.getBuildType()
          print "Running ${btype} build!"
          cmn.ci.abortPreviousRunningBuilds()
          /* Cleanup and Prep */
          cmn.prep(btype)
        }
      }
    }
    stage('Parallel') {
      parallel {
        stage('Checks') { stages {
          stage('Lint') {
            steps {
              script { cmn.nix.shell('lein cljfmt check', attr: 'targets.leiningen.shell') }
            }
          }
          stage('Tests') {
            steps {
              script { cmn.nix.shell('lein test-cljs', attr: 'targets.leiningen.shell') }
            }
          }
        } }
        stage('Build') { stages {
          stage('JSBundle') {
            steps {
              script { cmn.nix.shell('make jsbundle-ios') }
            }
          }
          stage('Bundle') {
            steps {
              script { api = ios.bundle() }
            }
          }
        } }
      }
    }
    stage('Archive') {
      steps {
        archiveArtifacts api
      }
    }
    stage('Upload') {
      steps {
        script {
          env.PKG_URL = cmn.utils.uploadArtifact(api)
          /* e2e builds get tested in SauceLabs */
          if (btype == 'e2e') {
            env.SAUCE_URL = ios.uploadToSauceLabs()
          } else {
            env.DIAWI_URL = ios.uploadToDiawi()
          }
        }
      }
    }
    stage('Cleanup') {
      steps {
        sh 'make watchman-clean'
        sh 'make clean'
      }
    }
  }
  post {
    success { script { load('ci/github.groovy').notifyPR(true) } }
    failure { script { load('ci/github.groovy').notifyPR(false) } }
    always { sh 'make _fix-node-perms' }
  }
}
