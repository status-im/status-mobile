library 'status-react-jenkins@v1.1.6'

pipeline {
  agent { label 'macos-xcode-11.4.1' }

  parameters {
    string(
      name: 'BUILD_TYPE',
      description: 'Specify build type. Values: pr / e2e / nightly / release',
      defaultValue: 'pr',
    )
  }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 45, unit: 'MINUTES')
    /* Limit builds retained */
    buildDiscarder(logRotator(
      numToKeepStr: '10',
      daysToKeepStr: '20',
      artifactNumToKeepStr: '10',
    ))
    /* Allows combined build to copy */
    copyArtifactPermission('/status-react/*')
  }

  environment {
    LANG     = "en_US.UTF-8"
    LC_ALL   = "en_US.UTF-8"
    LANGUAGE = "en_US.UTF-8"
    TARGET   = 'ios'
    NIX_CONF_DIR = "${env.WORKSPACE}/nix"
    /* See nix/README.md */
    NIX_IGNORE_SYMLINK_STORE = 1
    FASTLANE_DISABLE_COLORS = 1
    BUNDLE_PATH = "${HOME}/.bundle"
  }

  stages {
    stage('Prep') {
      steps {
        script {
          btype = utils.getBuildType()
          print "Running ${btype} build!"
          jenkins.abortPreviousRunningBuilds()
          /* Cleanup and Prep */
          commonPrep(btype)
        }
      }
    }
    stage('Parallel Assemble') {
      parallel {
        stage('Checks') { stages {
          stage('Lint') {
            steps {
              script {
                /* We want the build result to be uploaded */
                catchError(
                  message: 'Linting check failed!',
                  buildResult: 'FAILURE',
                  stageResult: 'FAILURE'
                ) {
                  sh 'make lint'
                }
              }
            }
          }
          stage('Tests') {
            steps {
              script {
                sh 'make test'
              }
            }
          }
        } }
        stage('Build') { stages {
          stage('JSBundle') {
            steps {
              script { sh 'make jsbundle-ios' }
            }
          }
          stage('Bundle') {
            steps {
              script { api = ios.bundle() }
            }
          }
        } }
      }
    }
    stage('Parallel Upload') {
      parallel {
        stage('Archive') {
          steps {
            archiveArtifacts api
          }
        }
        stage('Upload') {
          steps {
            script {
              env.PKG_URL = s3.uploadArtifact(api)
              /* e2e builds get tested in SauceLabs */
              if (btype == 'e2e') {
                env.SAUCE_URL = ios.uploadToSauceLabs()
              } else {
                env.DIAWI_URL = ios.uploadToDiawi()
              }
            }
          }
        }
      }
    }
    stage('Cleanup') {
      steps {
        sh 'make watchman-clean'
        sh 'make clean'
      }
    }
  }
  post {
    success { script { github.notifyPR(true) } }
    failure { script { github.notifyPR(false) } }
    always { sh 'make _fix-node-perms' }
  }
}
