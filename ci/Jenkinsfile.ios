pipeline {
  agent { label 'macos' }

  parameters {
    string(
      name: 'BUILD_TYPE',
      description: 'Specify build type. Values: pr / e2e / nightly / release',
      defaultValue: 'pr',
    )
  }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 45, unit: 'MINUTES')
    /* Limit builds retained */
    buildDiscarder(logRotator(
      numToKeepStr: '10',
      daysToKeepStr: '20',
      artifactNumToKeepStr: '10',
    ))
  }

  environment {
    LANG     = "en_US.UTF-8"
    LC_ALL   = "en_US.UTF-8"
    LANGUAGE = "en_US.UTF-8"
    TARGET_OS = 'ios'
    NIX_CONF_DIR = "${env.WORKSPACE}/nix"
    FASTLANE_DISABLE_COLORS = 1
    REALM_DISABLE_ANALYTICS = 1
    BUNDLE_PATH = "${HOME}/.bundle"
    /* We use EXECUTOR_NUMBER to avoid multiple instances clashing */
    LEIN_HOME         = "/var/tmp/lein-${EXECUTOR_NUMBER}"
    YARN_CACHE_FOLDER = "/var/tmp/yarn-${EXECUTOR_NUMBER}"
    GRADLE_USER_HOME  = "/var/tmp/gradle-${EXECUTOR_NUMBER}"
  }

  stages {
    stage('Prep') {
      steps {
        script {
          /* Necessary to load methods */
          mobile = load 'ci/mobile.groovy'
          cmn    = load 'ci/common.groovy'
          btype  = cmn.utils.getBuildType()
          print "Running ${btype} build!"
          cmn.ci.abortPreviousRunningBuilds()
          /* Cleanup and Prep */
          cmn.prep(btype)
        }
      }
    }
    stage('Lint') {
      steps {
        script { cmn.nix.shell('lein cljfmt check') }
      }
    }
    stage('Tests') {
      steps {
        script { cmn.nix.shell('lein test-cljs') }
      }
    }
    stage('Build') {
      steps {
        script {
          cmn.nix.shell('lein prod-build-ios')
          cmn.nix.shell('node prepare-modules.js')
        }
      }
    }
    stage('Bundle') {
      steps {
        script { api = mobile.ios.bundle() }
      }
    }
    stage('Archive') {
      steps {
        archiveArtifacts api
      }
    }
    stage('Upload') {
      steps {
        script {
          env.PKG_URL = cmn.utils.uploadArtifact(api)
          /* e2e builds get tested in SauceLabs */
          if (btype == 'e2e') {
            env.SAUCE_URL = mobile.ios.uploadToSauceLabs()
          } else {
            env.DIAWI_URL = mobile.ios.uploadToDiawi()
          }
        }
      }
    }
    stage('Cleanup') {
      steps {
        sh 'make clean'
      }
    }
  }
  post {
    success { script { load('ci/common.groovy').notifyPR(true) } }
    failure { script { load('ci/common.groovy').notifyPR(false) } }
  }
}
