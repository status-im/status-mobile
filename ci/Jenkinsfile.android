pipeline {
  agent { label 'linux' }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 45, unit: 'MINUTES')
    /* Limit builds retained */
    buildDiscarder(logRotator(
      numToKeepStr: '10',
      daysToKeepStr: '20',
      artifactNumToKeepStr: '10',
    ))
  }

  parameters {
    string(
      name: 'BUILD_TYPE',
      description: 'Specify build type. Values: pr / e2e / nightly / release',
      defaultValue: 'pr',
    )
  }

  environment {
    LANG     = "en_US.UTF-8"
    LC_ALL   = "en_US.UTF-8"
    LANGUAGE = "en_US.UTF-8"
    TARGET_OS = 'android'
    NIX_CONF_DIR = "${env.WORKSPACE}/nix"
    FASTLANE_DISABLE_COLORS = 1
    REALM_DISABLE_ANALYTICS = 1
    /* since we are mounting it we need to specify location */
    STATUS_RELEASE_STORE_FILE = '/home/jenkins/status-im.keystore'
    /* We use EXECUTOR_NUMBER to avoid multiple instances clashing */
    LEIN_HOME = "/var/tmp/lein-${EXECUTOR_NUMBER}"
  }

  stages {
    stage('Prep') {
      steps {
        script {
          /* Necessary to load methods */
          android = load 'ci/android.groovy'
          cmn     = load 'ci/common.groovy'
          btype   = cmn.utils.getBuildType()
          print "Running ${btype} build!"
          cmn.ci.abortPreviousRunningBuilds()
          /* Cleanup and Prep */
          cmn.prep(btype)
        }
      }
    }
    stage('Implicit dependencies') {
      steps {
        /* Build implicit dependencies if needed (we run `lein deps :tree` but it's not really required, for this purpose) */
        script { cmn.nix.shell('lein deps :tree') }
      }
    }
    stage('Parallel') {
      parallel {
        stage('Checks') { stages {
          stage('Lint') {
            steps {
              script { cmn.nix.shell('lein cljfmt check') }
            }
          }
          stage('Tests') {
            steps {
              script { cmn.nix.shell('lein test-cljs') }
            }
          }
        } }
        stage('Build') { stages {
          stage('Clojure') {
            steps {
              script { cmn.nix.build(attr: "targets.mobile.prod-build") }
            }
          }
          stage('Bundle') {
            steps {
              script { apk = android.bundle() }
            }
          }
        } }
      }
    }
    stage('Archive') {
      steps {
        archiveArtifacts apk
      }
    }
    stage('Upload') {
      steps {
        script {
          env.PKG_URL = cmn.utils.uploadArtifact(apk)
          /* build type specific */
          switch (btype) {
            case 'release':
              android.uploadToPlayStore(); break;
            case 'nightly':
              env.DIAWI_URL = android.uploadToDiawi(); break;
            case 'e2e':
              env.SAUCE_URL = android.uploadToSauceLabs()
          }
        }
      }
    }
    stage('Cleanup') {
      steps { sh 'make clean' }
    }
  }
  post {
    success { script { load('ci/github.groovy').notifyPR(true) } }
    failure { script { load('ci/github.groovy').notifyPR(false) } }
    always { sh 'make _fix-perms' }
  }
}
