pipeline {
  agent { 
    docker {
      label 'linux'
      image 'statusteam/nix:jenkins-1.0.0-150d5721'
      args (
        "-v /home/jenkins/tmp:/var/tmp:rw "+
        "-v /home/jenkins/status-im.keystore:/tmp/status-im.keystore:ro"
      )
    }
  }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 45, unit: 'MINUTES')
    /* Limit builds retained */
    buildDiscarder(logRotator(
      numToKeepStr: '10',
      daysToKeepStr: '20',
      artifactNumToKeepStr: '10',
    ))
  }

  parameters {
    string(
      name: 'BUILD_TYPE',
      description: 'Specify build type. Values: pr / e2e / nightly / release',
      defaultValue: 'pr',
    )
  }

  environment {
    TARGET_PLATFORM = 'android'
    LANG     = 'en_US.UTF-8'
    LANGUAGE = 'en_US.UTF-8'
    LC_ALL   = 'en_US.UTF-8'
    FASTLANE_DISABLE_COLORS = 1
    REALM_DISABLE_ANALYTICS = 1
    CI_ENVIRONMENT    = 'jenkins'
    /* since we are mounting it we need to specify location */
    STATUS_RELEASE_STORE_FILE = '/tmp/status-im.keystore'
    /* bundle cache is sensitive to being used by different ruby versions */
    BUNDLE_PATH       = "/var/tmp/bundle-${EXECUTOR_NUMBER}"
    /* We use EXECUTOR_NUMBER to avoid multiple instances clashing */
    LEIN_HOME         = "/var/tmp/lein-${EXECUTOR_NUMBER}"
    YARN_CACHE_FOLDER = "/var/tmp/yarn-${EXECUTOR_NUMBER}"
    GRADLE_USER_HOME  = "/var/tmp/gradle-${EXECUTOR_NUMBER}"
  }

  stages {
    stage('Prep') {
      steps {
        script {
          /* Necessary to load methods */
          mobile = load 'ci/mobile.groovy'
          cmn    = load 'ci/common.groovy'
          btype  = cmn.utils.getBuildType()
          print "Running ${btype} build!"
          cmn.ci.abortPreviousRunningBuilds()
          /* Cleanup and Prep */
          cmn.prep(btype)
        }
      }
    }
    stage('Lint') {
      steps {
        script { cmn.utils.nix_sh('lein cljfmt check') }
      }
    }
    stage('Tests') {
      steps {
        script { cmn.utils.nix_sh('lein test-cljs') }
      }
    }
    stage('Build') {
      steps {
        script { cmn.utils.nix_sh('lein prod-build-android') }
      }
    }
    stage('Bundle') {
      steps {
        script { apk = mobile.android.bundle(btype) }
      }
    }
    stage('Archive') {
      steps {
        archiveArtifacts apk
      }
    }
    stage('Upload') {
      steps {
        script {
          env.PKG_URL = cmn.utils.uploadArtifact(apk)
          /* build type specific */
          switch (btype) {
            case 'release':
              mobile.android.uploadToPlayStore(); break;
            case 'nightly':
              env.DIAWI_URL = mobile.android.uploadToDiawi(); break;
            case 'e2e':
              env.SAUCE_URL = mobile.android.uploadToSauceLabs()
          }
        }
      }
    }
    stage('Cleanup') {
      steps {
        sh 'make clean'
      }
    }
  }
  post {
    success { script { load('ci/common.groovy').notifyPR(true) } }
    failure { script { load('ci/common.groovy').notifyPR(false) } }
  }
}
