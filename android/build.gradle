import java.util.stream.Collectors

// Top-level build file where you can add configuration options common to all sub-projects/modules.

/**
* Project-wide gradle configuration properties for use by all modules
*/
ext {
    RNNKotlinVersion    = "1.4.31"
    buildToolsVersion   = project.buildToolsVersion
    minSdkVersion       = Integer.valueOf(project.minSdkVersion)
    compileSdkVersion   = Integer.valueOf(project.compileSdkVersion)
    targetSdkVersion    = Integer.valueOf(project.targetSdkVersion)
    supportLibVersion   = project.supportLibVersion
    gradlePluginVersion = project.gradlePluginVersion
}

def depsSet = new TreeSet<String>()

task printDependencies {
    doLast {
        println(depsSet.stream().sorted().collect(Collectors.toList()))
    }
}

buildscript {
    repositories {
        flatDir { dirs "libs", "${rootDir}/app/libs" }
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.31"
        classpath "com.android.tools.build:gradle:${project.ext.gradlePluginVersion}"
        // WARNING: Do not place your application dependencies here!
        //          They belong in the individual module build.gradle files.
    }
}

subprojects {
    afterEvaluate {
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion

                defaultConfig {
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }
            }
        }
    }
}

allprojects {
    beforeEvaluate {
        if (System.env.STATUS_GO_ANDROID_LIBDIR == null || System.env.STATUS_GO_ANDROID_LIBDIR == "") {
            throw new GradleException('STATUS_GO_ANDROID_LIBDIR environment variable is not valid!')
        }
    }

    afterEvaluate {
        project.configurations.all { config ->
            if (!config.name.toLowerCase().startsWith('test')) {
                config.allDependencies.all { dep ->
                    if (dep.group && dep.name && dep.version && dep.version != "unspecified") {
                        depsSet.add("${dep.group}:${dep.name}:${dep.version}")
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        maven { url("$rootDir/../node_modules/react-native/android") }
        // Android JSC is installed from npm
        maven { url("$rootDir/../node_modules/jsc-android/dist") }
        // For geth, function, and status-go
        flatDir { dirs "libs", System.env.STATUS_GO_ANDROID_LIBDIR }
        // Everything Else
        google()
        jcenter() // Required for @react-native-community/blur:3.6.1, can be removed after updating to 3.6.1+
        mavenCentral()
        maven { url "https://www.jitpack.io" }
    }
}
